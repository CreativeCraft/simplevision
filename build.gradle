plugins {
  id 'com.github.johnrengelman.shadow' version '7.1.1'
  id 'java'
}

group = 'org.creativecraft'
version = '1.0.0'

sourceCompatibility = '16'
targetCompatibility = '16'

repositories {
  mavenCentral()
  maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/public/' }
  maven { url = 'https://oss.sonatype.org/content/groups/public/' }
  maven { url = 'https://repo.aikar.co/content/groups/aikar/' }
  maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
  maven { url = 'https://repo.minebench.de/' }
  maven { url = 'https://maven.enginehub.org/repo/' }
}

dependencies {
  compileOnly 'org.spigotmc:spigot-api:1.18.1-R0.1-SNAPSHOT'
  compileOnly 'me.clip:placeholderapi:2.11.1'
  implementation 'co.aikar:acf-bukkit:0.5.0-SNAPSHOT'
  implementation 'de.themoep:minedown:1.7.0-SNAPSHOT'
  implementation 'org.bstats:bstats-bukkit-lite:1.8'
}

shadowJar {
  relocate 'co.aikar.commands', 'org.creativecraft.nightvision.lib.acf'
  relocate 'co.aikar.locales', 'org.creativecraft.nightvision.lib.locales'
  relocate 'de.themoep.minedown', 'org.creativecraft.nightvision.lib.minedown'
  relocate 'org.bstats', 'org.creativecraft.nightvision.lib.bstats'
}

processResources {
  def props = [version: version]
  inputs.properties props
  filteringCharset 'UTF-8'
  filesMatching('plugin.yml') {
    expand props
  }
}

build.dependsOn shadowJar
